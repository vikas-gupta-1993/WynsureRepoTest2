@ECHO OFF

SET CREATEUSERSGROUPSLINKSRETURNCODE=0
SET ACTIVEDIRECTORYPARAMETERS=ActiveDirectoryMainInformations.txt
SET CREATEUSERSGROUPSLINKSLOGFILE=CreateUsersGroupsLinks.log
SET CREATEUSERSGROUPSLINKSIMPORTFILE=CreateUsersGroupsLinks.csv

ECHO #INFO# Starting %~nx0 > %CREATEUSERSGROUPSLINKSLOGFILE%
IF NOT EXIST "%CREATEUSERSGROUPSLINKSIMPORTFILE%" CALL :FILENOTFOUNDERROR "%CREATEUSERSGROUPSLINKSIMPORTFILE%"
IF NOT EXIST "%ACTIVEDIRECTORYPARAMETERS%" CALL :FILENOTFOUNDERROR "%ACTIVEDIRECTORYPARAMETERS%"
CALL :READMAINPARAMETERS
CALL :CHECKPARAMETERS
IF NOT %CREATEUSERSGROUPSLINKSRETURNCODE%==0 GOTO ENDINERROR

FOR /F "tokens=1,2 delims=;" %%I IN (%CREATEUSERSGROUPSLINKSIMPORTFILE%) DO CALL :DSMODCMD "%%I" "%%J"
IF NOT %CREATEUSERSGROUPSLINKSRETURNCODE%==0 GOTO ENDINERROR
GOTO END

:READMAINPARAMETERS
FOR /F "tokens=1,2" %%I IN (%ACTIVEDIRECTORYPARAMETERS%) DO SET %%I=%%J
GOTO :EOF

:CHECKPARAMETERS
IF "%AD_WYNSUREDOMAIN%"=="" ECHO #ERROR# Parameter AD_WYNSUREDOMAIN needed and does not exist
IF "%AD_WYNUSER%"=="" ECHO #ERROR# Parameter AD_WYNUSER needed and does not exist
IF "%AD_WYNGROUP%"=="" ECHO #ERROR# Parameter AD_WYNGROUP needed and does not exist
IF "%AD_GROUPPREFIX%"=="" ECHO #ERROR# Parameter AD_GROUPPREFIX needed and does not exist
IF "%AD_GROUPPOSTFIX%"=="" ECHO #ERROR# Parameter AD_GROUPPOSTFIX needed and does not exist
IF "%AD_WYNSUREDOMAIN%"=="" SET CREATEUSERSGROUPSLINKSRETURNCODE=1
IF "%AD_WYNUSER%"=="" SET CREATEUSERSGROUPSLINKSRETURNCODE=1
IF "%AD_WYNGROUP%"=="" SET CREATEUSERSGROUPSLINKSRETURNCODE=1
IF "%AD_GROUPPREFIX%"=="" SET CREATEUSERSGROUPSLINKSRETURNCODE=1
IF "%AD_GROUPPOSTFIX%"=="" SET CREATEUSERSGROUPSLINKSRETURNCODE=1
GOTO :EOF

:DSMODCMD
SET GROUPSHORTDESC=%~1
SET USERNAME=%~2
SET USERNAME=%USERNAME%%AD_GROUPPOSTFIX%
ECHO #INFO# Adding link on group %GROUPSHORTDESC% to user %USERNAME%
ECHO #INFO# Adding link on group %GROUPSHORTDESC% to user %USERNAME%>> %CREATEUSERSGROUPSLINKSLOGFILE% 2>&1
dsmod group "cn=%AD_GROUPPREFIX%%GROUPSHORTDESC%%AD_GROUPPOSTFIX%,%AD_WYNGROUP%,%AD_WYNSUREDOMAIN%" -addmbr "cn=%USERNAME%,%AD_WYNUSER%,%AD_WYNSUREDOMAIN%">> %CREATEUSERSGROUPSLINKSLOGFILE% 2>&1
IF NOT %ERRORLEVEL%==0 SET CREATEUSERSGROUPSLINKSRETURNCODE=%ERRORLEVEL%
GOTO :EOF

:FILENOTFOUNDERROR
ECHO #ERROR# File not found : %1. Please check and retry.
SET CREATEUSERSGROUPSLINKSRETURNCODE=1
GOTO END

:ENDINERROR
ECHO ============= LOG CONTENT : ===================
TYPE %CREATEUSERSGROUPSLINKSLOGFILE%
ECHO .
ECHO ===============================================
ECHO #ERROR# %~nx0 ended in failure with return code %CREATEUSERSGROUPSLINKSRETURNCODE%. Please check the log %CREATEUSERSGROUPSLINKSLOGFILE%.
GOTO END

:END
ECHO End of %~nx0. Return code : %CREATEUSERSGROUPSLINKSRETURNCODE%
PAUSE
EXIT /B %CREATEUSERSGROUPSLINKSRETURNCODE%




