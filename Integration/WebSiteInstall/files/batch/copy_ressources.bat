SET COPYRESOURCESRETURNCODE=0
IF NOT "%COPYRESSOURCES%"=="Y" GOTO END

IF NOT EXIST "%VIRTUAL_DIR%" IF NOT "%VIRTUAL_DIR%"=="%WWEBRESOURCES%" CALL :COPYWWEBRESOURCES
IF EXIST "%VIRTUAL_DIR%" ECHO #INFO# folder "%VIRTUAL_DIR%", no need to copy Portal resources from "%WWEBRESOURCES%".
IF NOT EXIST "%WWEBRESOURCES%" GOTO SOURCENOTFOUND
GOTO END

:COPYWWEBRESOURCES
echo #INFO# Copying wWeb resources from "%WWEBRESOURCES%" to "%VIRTUAL_DIR%"
IF NOT EXIST "%VIRTUAL_DIR%" CALL :CREATEDIR "%VIRTUAL_DIR%"
CALL :COPYFOLDER css
CALL :COPYFOLDER doc
CALL :COPYFOLDER js
CALL :COPYFOLDER theme
CALL :COPYFILES index.html "%VIRTUAL_DIR%"
GOTO :EOF

:CREATEDIR
ECHO #INFO# Creating folder %1
MKDIR %1
IF NOT %ERRORLEVEL%==0 SET COPYRESOURCESRETURNCODE=%ERRORLEVEL%
GOTO :EOF

:SOURCENOTFOUND
ECHO #ERROR# Source folder not found : "%WWEBRESOURCES%"
SET COPYRESOURCESRETURNCODE=1
GOTO END

:COPYFOLDER
ECHO Copying %1
robocopy "%WWEBRESOURCES%\%1" "%VIRTUAL_DIR%\%1" /NDL /NFL /NP /R:5 /S >> %LOG% 2>&1
IF %ERRORLEVEL% GEQ 9 SET COPYRESOURCESRETURNCODE=%ERRORLEVEL%
GOTO :EOF

:COPYFILES
ECHO Copying %1
robocopy "%WWEBRESOURCES%" %2 %1 /NDL /NFL /NP /R:5 /S >> %LOG% 2>&1
IF %ERRORLEVEL% GEQ 9 SET COPYRESOURCESRETURNCODE=%ERRORLEVEL%
GOTO :EOF

:END
exit /b %COPYRESOURCESRETURNCODE%
