@echo off

SET INSTALLWPORTALRETURNCODE=0
SET PRODUCTNAME=wWeb
SET GSP_CONFIG_FILE=GSPNetConf.cfg
call %~dp0..\batch\config_env_wWeb.bat
IF NOT %ERRORLEVEL%==0 SET INSTALLWPORTALRETURNCODE=%ERRORLEVEL%
:::::::::::::::::::::::::::::::::::::::::::::::::::::::::
set BINVER=%1
set PORT=%2
set SERVER_PORT=%3
set PORTALDIRNAME=%4
set COPYMODE=%5
SET USERNAME=%6

IF "%BINVER%"=="" GOTO LAUNCHERROR
IF "%PORT%"=="" GOTO LAUNCHERROR
IF "%SERVER_PORT%"=="" GOTO LAUNCHERROR
IF "%PORTALDIRNAME%"=="" GOTO LAUNCHERROR
IF "%USERNAME%"=="" GOTO LAUNCHERROR

set SITENAME=%USERNAME%_%PRODUCTNAME%%PORT%
set APPPOOL=%USERNAME%_%PRODUCTNAME%AppPool%PORT%

set VIRTUAL_DIR=%WF-ROOT%\inetpub\wwwroot
::set VIRTUAL_DIR=%SITEDIR%\%PORTALDIRNAME%

::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
IF EXIST "%PORTALDIR%" (
	if /I "%COPYMODE%" EQU "override" (
		set COPYRESSOURCES=Y
	) ELSE (
		set COPYRESSOURCES=N
	)
) ELSE (
	set COPYRESSOURCES=Y
)
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

ECHO *********************************************
ECHO Main local variables :
ECHO PRODUCTNAME : %PRODUCTNAME%
ECHO USERNAME : %USERNAME%
ECHO BINVER : %BINVER%
ECHO PORT : %PORT%
ECHO SERVER_PORT : %SERVER_PORT%
ECHO PORTALDIRNAME : %PORTALDIRNAME%
ECHO COPYMODE : %COPYMODE%
ECHO SITENAME : %SITENAME%
ECHO APPPOOL : %APPPOOL%
ECHO VIRTUAL_DIR : %VIRTUAL_DIR%
ECHO SERVER_PORT_STR : %SERVER_PORT_STR%
ECHO INSTALLWPORTALROOTFOLDER : %INSTALLWPORTALROOTFOLDER%
ECHO ROOTWEBSITESFOLDER : %ROOTWEBSITESFOLDER%
ECHO GSPFOLDERSOURCE : %GSPFOLDERSOURCE%
ECHO GSPFOLDERTARGET : %GSPFOLDERTARGET%
ECHO GSPDLL : %GSPDLL%
ECHO WWEBRESOURCES : %WWEBRESOURCES%
ECHO SITEDIR : %SITEDIR%
ECHO VIRTUAL_DIR_LIST : %VIRTUAL_DIR_LIST%
ECHO BATCH : %BATCH%
ECHO GSP_HANDLER_EXT : %GSP_HANDLER_EXT%
ECHO GSP_HANDLER_NAME : %GSP_HANDLER_NAME%
ECHO GSP_CONFIG_FILE : %GSP_CONFIG_FILE%
ECHO *********************************************

call %BATCH%\create_log.bat
IF NOT %ERRORLEVEL%==0 SET INSTALLWPORTALRETURNCODE=%ERRORLEVEL%
call %BATCH%\check_env.bat
IF NOT %ERRORLEVEL%==0 SET INSTALLWPORTALRETURNCODE=%ERRORLEVEL%
IF NOT %INSTALLWPORTALRETURNCODE%==0 GOTO ERRORINSTALL
call %BATCH%\copy_ressources.bat
IF NOT %ERRORLEVEL%==0 SET INSTALLWPORTALRETURNCODE=%ERRORLEVEL%
call %BATCH%\launch_install.bat
IF NOT %ERRORLEVEL%==0 SET INSTALLWPORTALRETURNCODE=%ERRORLEVEL%
call %BATCH%\create_launchers.bat
IF NOT %ERRORLEVEL%==0 SET INSTALLWPORTALRETURNCODE=%ERRORLEVEL%
start %ENV-ROOT%\bin\%PRODUCTNAME%
IF NOT %INSTALLWPORTALRETURNCODE%==0 GOTO ERRORINSTALL
GOTO END

:ERRORINSTALL
echo #ERROR# Installation failed
GOTO END

:LAUNCHERROR
SET INSTALLWPORTALRETURNCODE=1
echo #ERROR# Launch error : install_wWeb [BINVER] [PORT] [SERVER_PORT] [PORTALDIRNAME] [COPYMODE] [USERNAME]
echo #ERROR# Please choose IIS Port, WSM Port, portal directory name, user name
GOTO END

:END
exit /B %INSTALLWPORTALRETURNCODE%

